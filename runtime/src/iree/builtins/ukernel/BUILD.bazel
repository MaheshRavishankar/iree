# Copyright 2022 The IREE Authors
#
# Licensed under the Apache License v2.0 with LLVM Exceptions.
# See https://llvm.org/LICENSE.txt for license information.
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

load("//build_tools/bazel:build_defs.oss.bzl", "iree_cmake_extra_content", "iree_runtime_cc_library")
load("//build_tools/bazel:iree_bitcode_library.bzl", "iree_bitcode_library", "iree_link_bitcode")
load("//build_tools/embed_data:build_defs.bzl", "c_embed_data")

load("//build_tools/bazel:currentdir_rule.bzl", "current_dir")

current_dir(name = "current_dir")

package(
    default_visibility = ["//visibility:public"],
    features = ["layering_check"],
    licenses = ["notice"],  # Apache 2.0
)

iree_runtime_cc_library(
    name = "static_assert",
    hdrs = ["static_assert.h"],
)

iree_runtime_cc_library(
    name = "exported_bits",
    hdrs = ["exported_bits.h"],
    deps = [":static_assert"],
)

internal_headers = [
    "common.h",
    "elementwise.h",
    "mmt4d.h",
    "mmt4d_internal.h",
    "pack.h",
    "pack_internal.h",
    "query_tile_sizes.h",
    "query_tile_sizes_internal.h",
    "unpack.h",
    "unpack_internal.h",
]

iree_runtime_cc_library(
    name = "internal_headers",
    hdrs = internal_headers,
    visibility = [":__subpackages__"],
    deps = [
        ":exported_bits",
        ":static_assert",
        "//runtime/src/iree/base:core_headers",
        "//runtime/src/iree/builtins/ukernel/arch:config",
    ],
)

# Entry points.
iree_runtime_cc_library(
    name = "ukernel",
    srcs = [
        "elementwise.c",
        "mmt4d.c",
        "mmt4d_tile.c",
        "pack.c",
        "pack_tile.c",
        "query_tile_sizes.c",
        "unpack.c",
        "unpack_tile.c",
    ] + internal_headers,
    hdrs = ["api.h"],
    deps = [
        ":exported_bits",
        ":static_assert",
        "//runtime/src/iree/base:core_headers",
        "//runtime/src/iree/builtins/ukernel/arch:config",
        "//runtime/src/iree/builtins/ukernel/arch:ukernel_arch",
    ],
)

#===------------------------------------------------------------------------===#
# UKernel bitcode files
#===------------------------------------------------------------------------===#

iree_cmake_extra_content(
    content = """
if(NOT IREE_BUILD_COMPILER OR NOT IREE_TARGET_BACKEND_LLVM_CPU)
  return()
endif()
""",
    inline = True,
)

BITCODE_BASE_COPTS = [
    # C17 with no system deps.
    "-std=c17",
    "-nostdinc",
    "-ffreestanding",

    # Optimized and unstamped.
    "-O3",
    "-fno-ident",
    "-fdiscard-value-names",

    # Object file only in bitcode format:
    "-c",
    "-emit-llvm",

    # Force the library into standalone mode (not linking into hosting apps).
    "-DIREE_DEVICE_STANDALONE=1",
    "-DNDEBUG",
]

UKERNEL_DEFAULT_HDRS = [
    "common.h",
    "mmt4d.h",
    "mmt4d_internal.h",
    "pack.h",
    "pack_internal.h",
    "query_tile_sizes.h",
    "query_tile_sizes_internal.h",
    "unpack.h",
    "unpack_internal.h",
]

UKERNEL_DEFAULT_SRCS = [
    "mmt4d.c",
    "mmt4d_tile.c",
    "pack.c",
    "pack_tile.c",
    "query_tile_sizes.c",
    "unpack_tile.c",
]

iree_bitcode_library(
    name = "ukernel_default",
    srcs = UKERNEL_DEFAULT_SRCS,
    hdrs = UKERNEL_DEFAULT_HDRS,
    copts = BITCODE_BASE_COPTS,
    header_paths = [
        "$(BINDIR)/runtime/src",
        "$(CURRENT_DIR)",
    ],
    data = [
        ":current_dir",
    ]
)


iree_link_bitcode(
    name = "ukernel_x86_64",
    bitcode_files = [
        ":ukernel_default",
        "//runtime/src/iree/builtins/ukernel/arch/x86_64:ukernel_x86_64_base",
        "//runtime/src/iree/builtins/ukernel/arch/x86_64:ukernel_x86_64_avx2_fma",
        "//runtime/src/iree/builtins/ukernel/arch/x86_64:ukernel_x86_64_avx512",
        "//runtime/src/iree/builtins/ukernel/arch/x86_64:ukernel_x86_64_avx512_vnni",
    ],
)

c_embed_data(
    name = "libukernel_x86_64",
    srcs = [
        ":ukernel_x86_64.bc",
    ],
    c_file_output = "libukernel.c",
    flatten = True,
    h_file_output = "libukernel.h",
    identifier = "iree_builtins_libukernel",
    deps = [
        "//runtime/src:runtime_defines",
    ],
)
